.define PUBLIC SERVICE_REQUEST 0
.define PUBLIC DATA_RECEIVED 1

.define BIT_RECEIVED 4

.program adb
.side_set 1 opt

.wrap_target
start:
  set pindirs, 1            side 1
  pull block

send_command:
  set x, 22             [1] side 0
attention:
  jmp x--, attention    [1]
sync:
  set x, 7              [3] side 1
command_byte:
  nop                   [1] side 0
  out pins, 1           [1]
  jmp x--, command_byte [1] side 1
command_stop:
  nop                   [3] side 0
  set pindirs, 0            side 1
service_request:
  jmp pin, command_sent
  irq SERVICE_REQUEST
  wait 1 pin 0
command_sent:
  out y, 8                           ; data length in bits - 1
  jmp !y, read_data

write_data:
  set pindirs, 1        [3] side 1
start_bit:
  nop                   [1] side 0
  nop                   [3] side 1
send_bits:
  nop                   [1] side 0
  out pins, 1           [1]           ; 2 - 8 bytes of data
  jmp y--, send_bits    [1] side 1
data_stop:
  nop                   [1] side 0
.wrap

read_data:
  wait 0 pin 0          [2]
  in pins, 1
  irq BIT_RECEIVED
  jmp read_data

.program data_timeout

.wrap_target
  wait 1 irq BIT_RECEIVED
bit_received:
  set x, 16
wait_for_bit:
  jmp pin, no_bit_yet
  jmp bit_received
no_bit_yet:
  jmp x--, wait_for_bit
bit_timeout:
  irq DATA_RECEIVED
.wrap
